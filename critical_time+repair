' --- Определение критического времени отключения КЗ с учётом ремонтных режимов ---

' --- Инициализация Excel ---
Set xl = CreateObject("Excel.Application")
xl.Visible = True
xl.DisplayAlerts = False
Set wb = xl.Workbooks.Add
Set wsData = wb.Worksheets(1)
wsData.Name = "Данные"
Set wsResult = wb.Worksheets.Add
wsResult.Name = "Результаты"
Set wsLog = wb.Worksheets.Add
wsLog.Name = "Логи"

Dim resultRow: resultRow = 1
Dim TemplateFieldsAdded: TemplateFieldsAdded = 0

MaxRepair = 2
MaxRepair_count = 1

' --- Ввод параметров КЗ ---
kzNodeInput = InputBox("Введите номер узла КЗ:", "Параметры КЗ", "1133")
If Not IsNumeric(kzNodeInput) Then
    MsgBox "Неверный номер узла КЗ."
    WScript.Quit
End If
Dim kzNode: kzNode = CInt(kzNodeInput)

kzTStartInput = InputBox("Введите время возникновения КЗ (мс):", "Параметры КЗ", "0")
If Not IsNumeric(kzTStartInput) Then
    MsgBox "Неверное время возникновения."
    WScript.Quit
End If
Dim kzTStart: kzTStart = CDbl(kzTStartInput)

kzBaseDurationInput = InputBox("Введите базовую длительность КЗ (мс):", "Параметры КЗ", "1000")
If Not IsNumeric(kzBaseDurationInput) Then
    MsgBox "Неверная базовая длительность."
    WScript.Quit
End If
Dim kzBaseDuration: kzBaseDuration = CDbl(kzBaseDurationInput)

' --- Глобальная переменная для индекса события КЗ ---
Dim g_ScenarioRowIndex: g_ScenarioRowIndex = -1

' --- Rastr таблицы ---
Set spBranch = Rastr.Tables("vetv")
Set spNode   = Rastr.Tables("node")
Set tblGen   = Rastr.Tables("Generator")

If tblGen Is Nothing Then
    MsgBox "Ошибка: Таблица Generator не найдена!", vbCritical, "Ошибка"
    WScript.Quit
End If

' --- Получение колонок ---
Set colGenName = tblGen.Cols("Name")
Set colGenNy   = tblGen.Cols("Node")
Set colGenSel  = tblGen.Cols("sel")
Set colGenP    = tblGen.Cols("P")
Set colGenPnom = tblGen.Cols("Pnom")

If colGenName Is Nothing Or colGenNy Is Nothing Or colGenP Is Nothing Then
    MsgBox "Ошибка: Не найдены необходимые колонки в таблице Generator!", vbCritical, "Ошибка"
    WScript.Quit
End If

' Проверка наличия поля sel
On Error Resume Next
Set colGenSel = tblGen.Cols("sel")
If Err.Number <> 0 Then
    tblGen.Cols.Add "sel", PR_BOOL
    tblGen.Cols("sel").Prop(FL_ZAG) = "Выбор"
    Set colGenSel = tblGen.Cols("sel")
    MsgBox "Добавлено поле sel. Выберите генераторы и запустите скрипт повторно.", vbInformation
    WScript.Quit
End If
On Error GoTo 0

' Получение колонок узлов
Set spNodeNy = spNode.Cols("ny")
Set spNodePg = spNode.Cols("pg")

If spNodePg Is Nothing Then
    MsgBox "Ошибка: Не найдена колонка генерации в таблице node!", vbCritical, "Ошибка"
    WScript.Quit
End If

' --- Логирование ---
Dim logRow: logRow = 1
Sub LogBinaryStep(powerLevel, phase, iteration, tMin, tMid, tMax, maxDelta, statusText, dynCode, snapIdx, scenario, repairDesc)
    wsLog.Cells(logRow, 1).Value = scenario
    wsLog.Cells(logRow, 2).Value = repairDesc
    wsLog.Cells(logRow, 3).Value = powerLevel
    wsLog.Cells(logRow, 4).Value = phase
    wsLog.Cells(logRow, 5).Value = iteration
    wsLog.Cells(logRow, 6).Value = tMin
    wsLog.Cells(logRow, 7).Value = tMid
    wsLog.Cells(logRow, 8).Value = tMax
    wsLog.Cells(logRow, 9).Value = maxDelta
    wsLog.Cells(logRow, 10).Value = statusText
    wsLog.Cells(logRow, 11).Value = dynCode
    wsLog.Cells(logRow, 12).Value = snapIdx
    logRow = logRow + 1
End Sub

' --- FWDynamic ---
Set spFWDynamic = Rastr.FWDynamic
If spFWDynamic Is Nothing Then
    MsgBox "Ошибка: FWDynamic недоступен!", vbCritical, "Ошибка"
    WScript.Quit
End If

Dim lastSnapIdx: lastSnapIdx = -1

' --- Функция пересчёта УР ---
Function SolveUR()
    SolveUR = Rastr.rgm("p")
End Function

' --- Функция получения MaxDelta ---
Function GetMaxDelta()
    Dim maxDelta: maxDelta = 0
    lastSnapIdx = -1
    wsData.Cells.Clear

    Dim dataCol: dataCol = 1

    For i = 0 To tblGen.Size - 1
        Plot = Empty
        npoints = 0

        For snapIdx = 500 To 0 Step -1
            On Error Resume Next
            Plot = Rastr.GetChainedGraphSnapshot("Generator", "Delta", i, snapIdx)
            errNum = Err.Number
            If errNum = 0 And IsArray(Plot) Then
                npoints = UBound(Plot, 1) + 1
                If npoints > 0 Then
                    lastSnapIdx = snapIdx
                    Exit For
                End If
            End If
            On Error GoTo 0
        Next

        If errNum = 0 And IsArray(Plot) And npoints > 0 Then
            wsData.Cells(2, dataCol).Resize(npoints, 2).Value = Plot

            deltaStartAddr = wsData.Cells(2, dataCol).Address(False, False, 1, True)
            deltaEndAddr = wsData.Cells(npoints + 1, dataCol).Address(False, False, 1, True)
            deltaRange = deltaStartAddr & ":" & deltaEndAddr

            wsData.Cells(2, dataCol + 2).Formula = "=MAX(" & deltaRange & ")"
            xl.Calculate

            maxGen = Abs(wsData.Cells(2, dataCol + 2).Value)
            If maxGen > maxDelta Then maxDelta = maxGen

            dataCol = dataCol + 3
        End If
    Next

    GetMaxDelta = maxDelta
End Function

' --- MaxDelta для конкретного генератора ---
Function GetGenMaxDelta(genIndex)
    Dim maxDelta: maxDelta = 0
    Plot = Empty
    npoints = 0
    wsData.Cells.Clear

    For snapIdx = 500 To 0 Step -1
        On Error Resume Next
        Plot = Rastr.GetChainedGraphSnapshot("Generator", "Delta", genIndex, snapIdx)
        errNum = Err.Number
        If errNum = 0 And IsArray(Plot) Then
            npoints = UBound(Plot, 1) + 1
            If npoints > 0 Then Exit For
        End If
        On Error GoTo 0
    Next

    If errNum = 0 And IsArray(Plot) And npoints > 0 Then
        wsData.Cells(2, 1).Resize(npoints, 2).Value = Plot

        deltaStartAddr = wsData.Cells(2, 1).Address(False, False, 1, True)
        deltaEndAddr = wsData.Cells(npoints + 1, 1).Address(False, False, 1, True)
        deltaRange = deltaStartAddr & ":" & deltaEndAddr

        wsData.Cells(2, 3).Formula = "=MAX(" & deltaRange & ")"
        xl.Calculate

        maxDelta = Abs(wsData.Cells(2, 3).Value)
    End If

    GetGenMaxDelta = maxDelta
End Function

' --- Функция перезагрузки сценария динамики ---
Sub ReloadDynamicScenario()
    On Error Resume Next
    spFWDynamic.LoadScenario()
    If Err.Number <> 0 Then
        Err.Clear
        Set spFWDynamic = Nothing
        Set spFWDynamic = Rastr.FWDynamic
    End If
    On Error GoTo 0
End Sub

' --- Функция создания/изменения КЗ в узле ---
Function CreateOrUpdateKZ(nodeNumber, tStart, tDuration)
    On Error Resume Next
    Set tblEvents = Rastr.Tables("DFWAutoActionScn")

    If tblEvents Is Nothing Then
        MsgBox "Ошибка: Таблица DFWAutoActionScn не найдена!", vbCritical
        CreateOrUpdateKZ = False
        Exit Function
    End If

    Set colId = tblEvents.Cols("Id")
    Set colParentId = tblEvents.Cols("ParentId")
    Set colType = tblEvents.Cols("Type")
    Set colName = tblEvents.Cols("Name")
    Set colFormula = tblEvents.Cols("Formula")
    Set colObjectClass = tblEvents.Cols("ObjectClass")
    Set colObjectProp = tblEvents.Cols("ObjectProp")
    Set colObjectKey = tblEvents.Cols("ObjectKey")
    Set colOutputMode = tblEvents.Cols("OutputMode")
    Set colRunsCount = tblEvents.Cols("RunsCount")
    Set colTimeStart = tblEvents.Cols("TimeStart")
    Set colDT = tblEvents.Cols("DT")
    Set colTag = tblEvents.Cols("Tag")
    Set colState = tblEvents.Cols("State")

    If g_ScenarioRowIndex >= 0 And g_ScenarioRowIndex < tblEvents.Size Then
        If InStr(colName.ZS(g_ScenarioRowIndex), "КЗ программное") > 0 Then
            colTimeStart.Z(g_ScenarioRowIndex) = tStart / 1000.0
            colDT.Z(g_ScenarioRowIndex) = tDuration / 1000.0
            colObjectKey.Z(g_ScenarioRowIndex) = nodeNumber
            CreateOrUpdateKZ = True
            Exit Function
        End If
    End If

    For i = 0 To tblEvents.Size - 1
        If InStr(colName.ZS(i), "КЗ программное") > 0 Then
            g_ScenarioRowIndex = i
            colTimeStart.Z(i) = tStart / 1000.0
            colDT.Z(i) = tDuration / 1000.0
            colObjectKey.Z(i) = nodeNumber
            CreateOrUpdateKZ = True
            Exit Function
        End If
    Next

    newRow = tblEvents.AddRow()
    g_ScenarioRowIndex = newRow

    maxId = 0
    For i = 0 To tblEvents.Size - 1
        If i <> newRow Then
            currentId = colId.Z(i)
            If currentId > maxId Then maxId = currentId
        End If
    Next

    colId.Z(newRow) = maxId + 1
    colParentId.Z(newRow) = 0
    colType.ZS(newRow) = "Объект"
    colName.ZS(newRow) = "КЗ программное - узел " & nodeNumber
    colFormula.ZS(newRow) = "0,001"
    colObjectClass.ZS(newRow) = "node"
    colObjectProp.ZS(newRow) = "x"
    colObjectKey.Z(newRow) = nodeNumber
    colOutputMode.Z(newRow) = 1
    colRunsCount.Z(newRow) = 1
    colTimeStart.Z(newRow) = tStart / 1000.0
    colDT.Z(newRow) = tDuration / 1000.0
    colTag.ZS(newRow) = "TrackID_" & (maxId + 1) & "_M_S"
    colState.Z(newRow) = 0

    CreateOrUpdateKZ = True
    On Error GoTo 0
End Function

' --- Функция двоичного поиска ---
Function BinarySearchCriticalTime(powerLevel, baseGenValues, selectedGenIndices, scenarioName, repairDesc)
    Dim tMin: tMin = 0.0
    Dim tMax: tMax = 1000.0
    Dim epsilon: epsilon = 5.0
    Dim tCritical: tCritical = -1
    Dim maxIterations: maxIterations = 50
    Dim iteration: iteration = 0

    For idx = 0 To UBound(selectedGenIndices)
        genIdx = selectedGenIndices(idx)
        colGenP.Z(genIdx) = baseGenValues(idx) * powerLevel / 100.0
    Next

    If SolveUR() <> 0 Then
        BinarySearchCriticalTime = -1
        Call LogBinaryStep(powerLevel, "min_error", "", tMin, "", tMax, "", "Ошибка при t=min", -1, -1, scenarioName, repairDesc)
        Exit Function
    End If

    If Not CreateOrUpdateKZ(kzNode, kzTStart, tMin) Then
        BinarySearchCriticalTime = -1
        Call LogBinaryStep(powerLevel, "min_kz_error", "", tMin, "", tMax, "", "Ошибка создания КЗ", -1, -1, scenarioName, repairDesc)
        Exit Function
    End If
    Call ReloadDynamicScenario()

    Dim dynRes: dynRes = spFWDynamic.Run()
    If dynRes <> 0 Then
        BinarySearchCriticalTime = -1
        Call LogBinaryStep(powerLevel, "min_dyn_error", "", tMin, "", tMax, "", "Ошибка динамики", dynRes, -1, scenarioName, repairDesc)
        Exit Function
    End If

    Dim maxDeltaMin: maxDeltaMin = GetMaxDelta()
    Dim stableAtMin: stableAtMin = (maxDeltaMin <= 360)
    Dim statusMin: If stableAtMin Then statusMin = "устойчив" Else statusMin = "нарушен"
    Call LogBinaryStep(powerLevel, "start_min", "", tMin, "", tMax, maxDeltaMin, statusMin, dynRes, lastSnapIdx, scenarioName, repairDesc)

    If Not stableAtMin Then
        BinarySearchCriticalTime = -999
        Call LogBinaryStep(powerLevel, "unstable_at_zero", "", tMin, "", "", maxDeltaMin, "КРИТИЧНО: нестабильно при t=0", dynRes, lastSnapIdx, scenarioName, repairDesc)
        Exit Function
    End If

    For idx = 0 To UBound(selectedGenIndices)
        genIdx = selectedGenIndices(idx)
        colGenP.Z(genIdx) = baseGenValues(idx) * powerLevel / 100.0
    Next

    If SolveUR() <> 0 Then
        BinarySearchCriticalTime = -1
        Call LogBinaryStep(powerLevel, "max_ur_error", "", tMin, "", tMax, "", "Ошибка УР", -1, -1, scenarioName, repairDesc)
        Exit Function
    End If

    If Not CreateOrUpdateKZ(kzNode, kzTStart, tMax) Then
        BinarySearchCriticalTime = -1
        Call LogBinaryStep(powerLevel, "max_kz_error", "", tMin, "", tMax, "", "Ошибка создания КЗ", -1, -1, scenarioName, repairDesc)
        Exit Function
    End If
    Call ReloadDynamicScenario()

    dynRes = spFWDynamic.Run()
    If dynRes <> 0 Then
        BinarySearchCriticalTime = -1
        Call LogBinaryStep(powerLevel, "max_dyn_error", "", tMin, "", tMax, "", "Ошибка динамики", dynRes, -1, scenarioName, repairDesc)
        Exit Function
    End If

    Dim maxDeltaMax: maxDeltaMax = GetMaxDelta()
    Dim unstableAtMax: unstableAtMax = (maxDeltaMax > 360)
    Dim statusMax: If unstableAtMax Then statusMax = "нарушен" Else statusMax = "устойчив"
    Call LogBinaryStep(powerLevel, "start_max", "", tMin, "", tMax, maxDeltaMax, statusMax, dynRes, lastSnapIdx, scenarioName, repairDesc)

    If Not unstableAtMax Then
        BinarySearchCriticalTime = 9999
        Call LogBinaryStep(powerLevel, "stable_at_max", "", "", "", tMax, maxDeltaMax, "Устойчиво при t=" & tMax, dynRes, lastSnapIdx, scenarioName, repairDesc)
        Exit Function
    End If

    Do While (tMax - tMin) > epsilon And iteration < maxIterations
        iteration = iteration + 1
        Dim tMid: tMid = (tMin + tMax) / 2.0

        For idx = 0 To UBound(selectedGenIndices)
            genIdx = selectedGenIndices(idx)
            colGenP.Z(genIdx) = baseGenValues(idx) * powerLevel / 100.0
        Next

        If SolveUR() <> 0 Then
            BinarySearchCriticalTime = -1
            Call LogBinaryStep(powerLevel, "binary_ur_error", iteration, tMin, tMid, tMax, "", "Ошибка УР", -1, -1, scenarioName, repairDesc)
            Exit Function
        End If

        If Not CreateOrUpdateKZ(kzNode, kzTStart, tMid) Then
            BinarySearchCriticalTime = -1
            Call LogBinaryStep(powerLevel, "binary_kz_error", iteration, tMin, tMid, tMax, "", "Ошибка создания КЗ", -1, -1, scenarioName, repairDesc)
            Exit Function
        End If

        Call ReloadDynamicScenario()

        dynRes = spFWDynamic.Run()
        If dynRes <> 0 Then
            BinarySearchCriticalTime = -1
            Call LogBinaryStep(powerLevel, "binary_dyn_error", iteration, tMin, tMid, tMax, "", "Ошибка динамики", dynRes, -1, scenarioName, repairDesc)
            Exit Function
        End If

        Dim maxDeltaMid: maxDeltaMid = GetMaxDelta()
        Dim stableNow: stableNow = (maxDeltaMid <= 360)
        Dim statusNow: If stableNow Then statusNow = "устойчив" Else statusNow = "нарушен"
        Call LogBinaryStep(powerLevel, "binary", iteration, tMin, tMid, tMax, maxDeltaMid, statusNow, dynRes, lastSnapIdx, scenarioName, repairDesc)

        If stableNow Then
            tMin = tMid
        Else
            tMax = tMid
        End If
    Loop

    tCritical = tMin
    Call LogBinaryStep(powerLevel, "final", iteration, tMin, tCritical, tMax, "", "РЕЗУЛЬТАТ: tКрит=" & FormatNumber(tCritical, 3), 0, lastSnapIdx, scenarioName, repairDesc)

    BinarySearchCriticalTime = tCritical
End Function

' --- Класс Combinator ---
Class Combinator
    Dim Counter()
    Dim V()
    Private m
    Private n
    Private Sub Swap(byref V,i1,i2)
        temp  = V(i1)
        V(i1) = V(i2)
        V(i2) = temp
    End Sub
    public function Init(Vsource,Em,En)
        Init = True
        m = Em
        n = En
        Redim V(UBound(Vsource))
        for i = 0 to UBound(Vsource)
            V(i) = Vsource(i)
        next
        if m > n Then MsgBox "M>N !" : Init = False
        If Init Then
            Redim Counter(n-1)
            for i = 0 to Ubound(Counter)
                Counter(i) = i
            next
        End If
    end function
    public function FirstCombination(byref Vout)
        ReDim Vout(m-1)
        for i = 0 to m-1
            Vout(i) = V(Counter(i))
        next
        FirstCombination = not ((m > n) or (m = 0))
    end function
    public function NextCombination(byref Vout)
        dim incremented
        incremented = false
        if Counter(m-1) < n - 1 Then
            Counter(m-1) = Counter(m-1) + 1
            incremented = true
        Else
            for i = m-2 to 0 step -1
                if Counter(i) < n - m + i Then
                    Counter(i) = Counter(i) + 1
                    incremented = true
                    for j = i + 1 to m - 1
                        Counter(j) = Counter(i) + j - i
                    next
                    exit for
                End If
            next
        End if
        if incremented then
            Redim Vout(m-1)
            for i = 0 to m-1
                Vout(i) = V(Counter(i))
            next
            NextCombination = 1
        else
            NextCombination = 0
        end if
    end function
End Class

' --- Topology Store/Restore ---
Sub TopologyStore
    On Error Resume Next
    spNode.Cols.Add "staRes",PR_BOOL
    spBranch.Cols.Add "staRes",PR_BOOL
    On Error GoTo 0
    spNode.SetSel ""
    spNode.Cols("staRes").Calc("sta")
    spBranch.SetSel ""
    spBranch.Cols("staRes").Calc("sta")
End Sub

Sub TopologyRestore
    spNode.SetSel ""
    spNode.Cols("sta").Calc("staRes")
    spBranch.SetSel ""
    spBranch.Cols("sta").Calc("staRes")
End Sub

' --- Prepare Template ---
Sub PrepareTemplate
    TemplateFieldsAdded = 0
    If spBranch.Cols.Find("Repair") < 0 Then
        spBranch.Cols.Add "Repair", PR_BOOL
        spBranch.Cols("Repair").Prop(FL_ZAG) = "Ремонт"
        TemplateFieldsAdded = 1
    End If
    If spBranch.Cols.Find("_SortKey") < 0 Then
        spBranch.Cols.Add "_SortKey", PR_INT
        TemplateFieldsAdded = 1
    End If
    If TemplateFieldsAdded = 1 Then MsgBox "Добавлены поля Repair/_SortKey. Сохраните модель, если нужно.", 1, "Подготовка шаблона"
End Sub

' --- Place Offs ---
Function PlaceOffs(byref V, OffCounter)
    OffList = ""
    for offc = 0 to OffCounter-1
        spBranch.Cols("sta").Z(V(offc)) = 1
        GroupId = spBranch.Cols("groupid").Z(V(offc))
        if GroupId > 0 Then
            spBranch.SetSel "groupid=" & CStr(GroupId)
            spBranch.Cols("sta").Calc 1
            spBranch.SetSel ""
        End If
        if OffList <> "" Then OffList = OffList + "|"
        OffList = OffList + Cstr(spBranch.Cols("name").ZS(V(offc)))
    next
    PlaceOffs = OffList
End Function

' --- Refresh Repair Indexes ---
Dim RepairIndexes()
Sub RefreshRepairIndexes()
    spBranch.SetSel("Repair")
    ReDim RepairIndexes(spBranch.Count)
    cnt = 0
    ndx = spBranch.FindNextSel(-1)
    While ndx >= 0
        RepairIndexes(cnt) = ndx
        cnt = cnt + 1
        ndx = spBranch.FindNextSel(ndx)
    Wend
    If cnt > 0 Then
        ReDim Preserve RepairIndexes(cnt-1)
    Else
        ReDim RepairIndexes(-1)
    End If
    totalRepairs = cnt
    If MaxRepair > totalRepairs Then MaxRepair = totalRepairs
    If MaxRepair_count < 0 Then MaxRepair_count = 0
    If MaxRepair_count > MaxRepair Then MaxRepair_count = MaxRepair
End Sub

' --- Основная логика ---

PrepareTemplate

Dim selectedGenIndices()
Dim selectedNodeIndices()
Dim originalGenValues()
Dim basePgValues()
ReDim selectedGenIndices(-1)
ReDim selectedNodeIndices(-1)
ReDim originalGenValues(-1)
ReDim basePgValues(-1)

tblGen.SetSel("sel")
genIdx = tblGen.FindNextSel(-1)
While genIdx >= 0
    ReDim Preserve selectedGenIndices(UBound(selectedGenIndices) + 1)
    selectedGenIndices(UBound(selectedGenIndices)) = genIdx

    nodeNum = colGenNy.Z(genIdx)
    nodeIdx = -1
    For i = 0 To spNode.Size - 1
        If spNodeNy.Z(i) = nodeNum Then
            nodeIdx = i
            Exit For
        End If
    Next

    If nodeIdx >= 0 Then
        ReDim Preserve selectedNodeIndices(UBound(selectedNodeIndices) + 1)
        selectedNodeIndices(UBound(selectedNodeIndices)) = nodeIdx

        ReDim Preserve originalGenValues(UBound(originalGenValues) + 1)
        originalGenValues(UBound(originalGenValues)) = colGenP.Z(genIdx)

        ReDim Preserve basePgValues(UBound(basePgValues) + 1)
        Dim pVal
        pVal = colGenPnom.Z(genIdx)
        If IsNumeric(pVal) And Not IsNull(pVal) Then
            basePgValues(UBound(basePgValues)) = CDbl(pVal)
        Else
            basePgValues(UBound(basePgValues)) = colGenP.Z(genIdx)
        End If
    End If

    genIdx = tblGen.FindNextSel(genIdx)
Wend

If UBound(selectedGenIndices) < 0 Then
    MsgBox "Не выбрано ни одного генератора!", vbExclamation, "Нет выбора"
    WScript.Quit
End If

wsResult.Cells(resultRow, 1).Value = "Сценарий"
wsResult.Cells(resultRow, 2).Value = "Режим"
wsResult.Cells(resultRow, 3).Value = "Уровень мощности (%)"
wsResult.Cells(resultRow, 4).Value = "Генератор"
wsResult.Cells(resultRow, 5).Value = "Max Delta"
wsResult.Cells(resultRow, 6).Value = "Статус"
wsResult.Cells(resultRow, 7).Value = "Критическое время отключения КЗ (мс)"
wsResult.Range(wsResult.Cells(resultRow, 1), wsResult.Cells(resultRow, 7)).Font.Bold = True
resultRow = resultRow + 1

wsLog.Cells(1, 1).Value = "Сценарий"
wsLog.Cells(1, 2).Value = "Ремонт"
wsLog.Cells(1, 3).Value = "Мощность %"
wsLog.Cells(1, 4).Value = "Фаза"
wsLog.Cells(1, 5).Value = "Итерация"
wsLog.Cells(1, 6).Value = "t_min"
wsLog.Cells(1, 7).Value = "t_mid"
wsLog.Cells(1, 8).Value = "t_max"
wsLog.Cells(1, 9).Value = "MaxDelta"
wsLog.Cells(1, 10).Value = "Статус"
wsLog.Cells(1, 11).Value = "DynCode"
wsLog.Cells(1, 12).Value = "SnapIdx"
wsLog.Rows(1).Font.Bold = True
logRow = 2

Dim powerLevels(4)
powerLevels(0) = 60
powerLevels(1) = 70
powerLevels(2) = 80
powerLevels(3) = 90
powerLevels(4) = 100

Dim RepairComb
Set RepairComb = new Combinator
Dim RepComb()

' Текущий сценарий
scenarioName = "Текущий сценарий"

TopologyStore
RefreshRepairIndexes

' Нормальная схема
repairDesc = "Нормальная схема"
RunCriticalTimeForMode scenarioName, repairDesc, powerLevels, basePgValues, selectedGenIndices

' Ремонты
If UBound(RepairIndexes) >= 0 Then
    totalRepairs = UBound(RepairIndexes) + 1
    For RepOff = MaxRepair_count To MaxRepair
        RepairComb.Init RepairIndexes, RepOff, totalRepairs
        If RepairComb.FirstCombination(RepComb) Then
            Do
                RepTitle = PlaceOffs(RepComb, RepOff)
                repairDesc = "Ремонт ВЛ - " & RepTitle
                RunCriticalTimeForMode scenarioName, repairDesc, powerLevels, basePgValues, selectedGenIndices
                TopologyRestore
            Loop While RepairComb.NextCombination(RepComb)
        End If
    Next
End If

Sub RunCriticalTimeForMode(scenarioName, repairDesc, powerLevels, basePgValues, selectedGenIndices)
    wsResult.Cells(resultRow, 1).Value = scenarioName
    wsResult.Cells(resultRow, 2).Value = repairDesc
    wsResult.Cells(resultRow, 1).Font.Bold = True
    wsResult.Cells(resultRow, 2).Font.Bold = True
    resultRow = resultRow + 1

    For pIdx = 0 To UBound(powerLevels)
        powerLevel = powerLevels(pIdx)

        For idx = 0 To UBound(selectedGenIndices)
            genIdx = selectedGenIndices(idx)
            colGenP.Z(genIdx) = basePgValues(idx) * powerLevel / 100.0
        Next

        urRes = SolveUR()
        If urRes = 0 Then
            If Not CreateOrUpdateKZ(kzNode, kzTStart, kzBaseDuration) Then
                wsResult.Cells(resultRow, 1).Value = ""
                wsResult.Cells(resultRow, 2).Value = ""
                wsResult.Cells(resultRow, 3).Value = powerLevel & "%"
                wsResult.Cells(resultRow, 4).Value = ""
                wsResult.Cells(resultRow, 5).Value = ""
                wsResult.Cells(resultRow, 6).Value = "Ошибка создания базового КЗ"
                wsResult.Cells(resultRow, 7).Value = "Ошибка"
                Call LogBinaryStep(powerLevel, "base_kz_err", "", "", "", "", "", "Ошибка создания КЗ", -1, -1, scenarioName, repairDesc)
                resultRow = resultRow + 1
            Else
                Call ReloadDynamicScenario()
                dynRes = spFWDynamic.Run()
                If dynRes = 0 Then
                    ReDim genMaxDeltas(UBound(selectedGenIndices))
                    anyViolation = False
                    For g = 0 To UBound(selectedGenIndices)
                        genIdx = selectedGenIndices(g)
                        genMaxDeltas(g) = GetGenMaxDelta(genIdx)
                        If genMaxDeltas(g) > 360 Then anyViolation = True
                    Next

                    maxDelta = GetMaxDelta()
                    stableNow = (maxDelta <= 360)
                    If stableNow Then statusBase = "устойчив" Else statusBase = "нарушен"
                    Call LogBinaryStep(powerLevel, "base", "", "", "", "", maxDelta, statusBase, dynRes, lastSnapIdx, scenarioName, repairDesc)

                    For idx = 0 To UBound(selectedGenIndices)
                        genIdx = selectedGenIndices(idx)
                        colGenP.Z(genIdx) = basePgValues(idx) * powerLevel / 100.0
                    Next
                    urRes = SolveUR()

                    If anyViolation Then
                        tCritical = BinarySearchCriticalTime(powerLevel, basePgValues, selectedGenIndices, scenarioName, repairDesc)
                        If IsNumeric(tCritical) And tCritical >= 0 Then
                            tCriticalForRows = FormatNumber(CDbl(tCritical)/1000, 3)
                        Else
                            tCriticalForRows = "Ошибка поиска"
                        End If
                    Else
                        tCriticalForRows = "> 1000.0"
                    End If

                    For g = 0 To UBound(selectedGenIndices)
                        genIdx = selectedGenIndices(g)
                        genName = colGenName.ZS(genIdx)
                        wsResult.Cells(resultRow, 1).Value = ""
                        wsResult.Cells(resultRow, 2).Value = ""
                        wsResult.Cells(resultRow, 3).Value = powerLevel & "%"
                        wsResult.Cells(resultRow, 4).Value = genName
                        wsResult.Cells(resultRow, 5).Value = genMaxDeltas(g)
                        If genMaxDeltas(g) > 360 Then
                            wsResult.Cells(resultRow, 6).Value = "Нарушение устойчивости"
                        Else
                            wsResult.Cells(resultRow, 6).Value = "Устойчивость сохранена"
                        End If
                        wsResult.Cells(resultRow, 7).Value = tCriticalForRows
                        resultRow = resultRow + 1
                    Next
                Else
                    wsResult.Cells(resultRow, 1).Value = ""
                    wsResult.Cells(resultRow, 2).Value = ""
                    wsResult.Cells(resultRow, 3).Value = powerLevel & "%"
                    wsResult.Cells(resultRow, 4).Value = ""
                    wsResult.Cells(resultRow, 5).Value = ""
                    wsResult.Cells(resultRow, 6).Value = "Ошибка расчёта динамики"
                    wsResult.Cells(resultRow, 7).Value = "Ошибка (код " & dynRes & ")"
                    Call LogBinaryStep(powerLevel, "base_dyn_err", "", "", "", "", "", CStr("dyn_err"), dynRes, -1, scenarioName, repairDesc)
                    resultRow = resultRow + 1
                End If
            End If
        Else
            wsResult.Cells(resultRow, 1).Value = ""
            wsResult.Cells(resultRow, 2).Value = ""
            wsResult.Cells(resultRow, 3).Value = powerLevel & "%"
            wsResult.Cells(resultRow, 4).Value = ""
            wsResult.Cells(resultRow, 5).Value = ""
            wsResult.Cells(resultRow, 6).Value = "Ошибка расчёта УР"
            wsResult.Cells(resultRow, 7).Value = "Ошибка (код " & urRes & ")"
            Call LogBinaryStep(powerLevel, "base_ur_err", "", "", "", "", "", CStr("ur_err"), urRes, -1, scenarioName, repairDesc)
            resultRow = resultRow + 1
        End If
    Next
End Sub

MsgBox "Расчёт завершён! Результаты в Excel.", vbInformation, "Готово"
